use std::str::FromStr;
use crate::ast::*;
use lalrpop_util::ParseError;
use anyhow::Error;

grammar;

extern {
    type Error = Error;
}

Comma<T>: Vec<T> = { 
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

SemiComma<T>: Vec<T> = { 
    <v:(<T> ";")*> => v
};

Vec<T>: Vec<T> = {
    <v:(<T>)*> => v
};

pub Pars  = Comma<Par>;

Par : Identifier = {
    <id:Id> ":" <dat:DataType> => {
        id.update_ty(dat)
    }
};
pub Program : Program = <decls:Vec<Decl>> => {
    Program {
        functions: decls.clone().into_iter()
                        .filter_map(|w| match w { Decl::Func(f) => Some(f), _ => None })
                        .collect(),
        structs: decls.clone().into_iter()
                        .filter_map(|w| match w { Decl::Struct(f) => Some(f), _ => None })
                        .collect()
    }
};

pub Decl: Decl = {
    <f:Funcdef> => Decl::Func(f),
    <s:Struct> => Decl::Struct(s)
};

pub Funcdef : Func = {
	"fn" <id:Id> "(" <pars:Pars> ")" "{" <statements:Statements> "}" =>? Func::new(id, pars, statements).map_err(|w| ParseError::User {
        error: w
    })
};

pub Struct : Struct = {
    "struct" <id:Id> "{" <fields:Comma<Field>> "}" =>? Struct::new(id, fields).map_err(|w| ParseError::User {
        error: w
    })
};

Field: Field = {
    <id:Id> ":" <dat:DataType> => Field::new(id, dat)
};

DataType: DataType = {
    "int" => DataType::Int,
    <id:Id> => DataType::Struct(Box::new(id))
}

pub Statements = SemiComma<Statement>;

pub Statement: Box<Statement> = {
	"return" <e:Expr> => Box::new(Statement::Ret(e)),
    "let" <id:Id> ":" <dat:DataType> "=" <e:Expr> => {
        Box::new(Statement::Assign(id.update_ty(dat), e))
    },
    <id:Id> ":" <cond:Conditional> => Box::new(Statement::Conditional(Some(id), cond)),
    <cond:Conditional> => Box::new(Statement::Conditional(None, cond)),
    <cond:Conditional> <id:Id> ":" => Box::new(Statement::Conditional(Some(id), cond)),
    <id:Id> "=" <e:Expr> => Box::new(Statement::ReAssign(id, e)),
    <id:Id> "." <field:Id> "=" <e:Expr> => 
        Box::new(Statement::ReAssign(id.update_field_access(Some(field)), e)),
	"let" <id:Id> "=" "new" <st:Id> =>
        Box::new(Statement::Allocate(id.update_ty(DataType::Struct(Box::new(st.clone()))), st))
};

pub Conditional: Vec<Box<Guard>> = {
    "match" <guards:SemiComma<Guard>> "end" => guards
};

pub Guard: Box<Guard> = {
    <e:Expr> "->" <stats:Statements> <cont:Continuation> => Box::new(Guard {
        guard: Some(e),
        statements: stats,
        continuation: cont,
    }),
    "_" "->" <stats:Statements> <cont:Continuation> => Box::new(Guard {
        guard: None,
        statements: stats,
        continuation: cont,
    })
};

Continuation: Continuation = {
    "continue" => Continuation::Continue,
    "break" => Continuation::Break,
};

pub Expr: Box<Expr> = {
    <n1:Term> <op:BinOp> <n2:Term> => Box::new(Expr::Dual(op, n1, n2)),
    "(" <n1:Term> <op:BinOp> <n2:Term> ")" => Box::new(Expr::Dual(Opcode::Geq, n1, n2)),
};

Exprs: Vec<Box<Expr>> = Comma<Expr>;

BinOp: Opcode = {
    "==" => Opcode::Cmp
};

pub Term: Box<Term> = {
	Num => Box::new(Term::Num(<>)),
	Id => Box::new(Term::Id(<>)),
    <obj:Id> "." <field:Id> => 
        Box::new(Term::Object(obj.update_field_access(Some(field.clone())), field)),
	"(" <n:Term> ")" => n,
	<id:Id> "(" <exprs:Exprs> ")" => Box::new(Term::Call(id, exprs)),
};

pub Id: Identifier = <l:@L> <s:r"[a-zA-Z][a-zA-Z0-9]*"> <r:@R> => Identifier::new(s.to_string(), l, r, None);
Num: i64= <s:r"[0-9]+"> => i64::from_str(s).unwrap();
